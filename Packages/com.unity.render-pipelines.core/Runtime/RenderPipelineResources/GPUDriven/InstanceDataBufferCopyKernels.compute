
#pragma kernel MainCopyInstances

//#pragma enable_d3d11_debug_symbols

int _InputValidComponentCounts;
int _InstanceCounts;
ByteAddressBuffer _ValidComponentIndices;
ByteAddressBuffer _ComponentByteCounts;
ByteAddressBuffer _InputComponentAddresses;
ByteAddressBuffer _OutputComponentAddresses;
ByteAddressBuffer _InputBuffer;
RWByteAddressBuffer _OutputBuffer;

[numthreads(64,1,1)]
void MainCopyInstances(uint3 dispatchThreadID : SV_DispatchThreadID)
{
    uint instanceIndex = dispatchThreadID.x;
    if (instanceIndex >= (uint)_InstanceCounts)
        return;

    [loop]
    for (int validComponentIndex = 0; validComponentIndex < _InputValidComponentCounts; ++validComponentIndex)
    {
        uint componentIndex = _ValidComponentIndices.Load(validComponentIndex << 2);
        uint inputComponentAddress = _InputComponentAddresses.Load(componentIndex << 2);
        uint outputComponentAddress = _OutputComponentAddresses.Load(componentIndex << 2);
        uint componentByteSize = _ComponentByteCounts.Load(componentIndex << 2);
        for (uint uintIndex = 0; uintIndex < componentByteSize / 4u; ++uintIndex)
        {
            uint byteOffset = instanceIndex * componentByteSize + uintIndex * 4;
            uint outputAddress = outputComponentAddress + byteOffset;
            uint inputAddress  = inputComponentAddress + byteOffset;
            _OutputBuffer.Store(outputAddress, _InputBuffer.Load(inputAddress));
        }
    }
}
